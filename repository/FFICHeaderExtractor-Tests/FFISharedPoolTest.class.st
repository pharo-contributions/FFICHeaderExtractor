Class {
	#name : #FFISharedPoolTest,
	#superclass : #TestCase,
	#category : 'FFICHeaderExtractor-Tests'
}

{ #category : #tests }
FFISharedPoolTest >> testChangePlatformAndTestingMethods [
	self testingSharedPool reset; updatePlatformInfo.
	self deny: self testingSharedPool platformName isNil.
	self deny: self testingSharedPool wordSize isNil.
	self assert: self testingSharedPool hasAlreadyBeenInitializedBefore.
	self testingSharedPool platformName: 'whatever'. 
	self assert: self testingSharedPool shouldInitializeVariables.
	self assert: self testingSharedPool didPlatformChange.
	
]

{ #category : #tests }
FFISharedPoolTest >> testInitializeVariables [
	self testingSharedPool 
		reset;
		extractAndStoreHeadersInformation.
	self assert: self testingSharedPool shouldInitializeVariables.
	self testingSharedPool initializeVariablesIfNeeded. 
	self assert: self testingSharedPool platformName equals: FFISharedPool currentPlatformName.
	self assert: self testingSharedPool wordSize equals: FFISharedPool currentWordSize.
	self assert: (self testingSharedPool classVarNamed: 'SIGKILL') equals: 9.
	self assert: (self testingSharedPool classVarNamed: 'EINVAL') equals: 22.
	self assert: (self testingSharedPool classVarNamed: 'WHATEVER') isNil.


	
	
]

{ #category : #tests }
FFISharedPoolTest >> testInitializeVariablesWithNoMethodFound [
	self testingSharedPool reset.
	self testingSharedPool removeAllGeneratedMethods.
	self testingSharedPool classVarNamed: 'SIGKILL' put: 42.
	self assert: self testingSharedPool shouldInitializeVariables.
	self testingSharedPool initializeVariablesIfNeeded.
	self assert: self testingSharedPool platformName isNil.
	self assert: self testingSharedPool wordSize isNil.
	"Class variables are untouched if the initialize does not run"
	self assert: (self testingSharedPool classVarNamed: 'SIGKILL') equals: 42.


	
	
]

{ #category : #tests }
FFISharedPoolTest >> testLookupInitMethodForCurrentPlatform [
	self testingSharedPool 
		reset;
		extractAndStoreHeadersInformation.
	self testingSharedPool class compile: 'initVariablesWhatever32
		<platformName: ''Whatever'' wordSize: 4>
	EACCES := 13.
	EINVAL := 22.
	SIGKILL := 9.
	SIGTERM := 15.
	"WHATEVER is UNDEFINED for this platform"
	' classified: 'autogenerated by FFICHeaderExtractor'.
	self assert: self testingSharedPool cHeaderExtractor autogeneratedMethodName asString equals: self testingSharedPool lookupInitMethodForCurrentPlatform asString. 
	
]

{ #category : #tests }
FFISharedPoolTest >> testRemoveAllGeneratedMethods [
	self testingSharedPool 
		reset; 
		extractAndStoreHeadersInformation.
	self assert: self testingSharedPool  allAutogeneratedMethodSelectors size >= 1.	
	self testingSharedPool removeAllGeneratedMethods.
	self assert: self testingSharedPool  allAutogeneratedMethodSelectors isEmpty.	
	
]

{ #category : #tests }
FFISharedPoolTest >> testResetAndTestingMethods [
	self testingSharedPool classVarNamed: 'SIGKILL' put: 9.
	self testingSharedPool reset. 
	"Class variables values are not reset"
	self assert: (self testingSharedPool classVarNamed: 'SIGKILL') isNil not.
	self assert: self testingSharedPool platformName isNil.
	self assert: self testingSharedPool wordSize isNil.
	self deny: self testingSharedPool hasAlreadyBeenInitializedBefore.
	self assert: self testingSharedPool shouldInitializeVariables.
]

{ #category : #accessing }
FFISharedPoolTest >> testingSharedPool [
	^ FFITestingSharedPool 
]
